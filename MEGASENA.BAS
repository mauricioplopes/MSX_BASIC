1 REM PROGRAMA DESENVOLVIDO POR
2 REM MAURICIOPLOPES@YAHOO.COM.BR
3 REM DEZ/2024
4 REM ---------------------------
5 CLS
10 PRINT "SORTEIO DE NUMEROS PARA MEGASENA"
20 PRINT "QUANTOS NUMEROS TERA SUA APOSTA?"
30 INPUT "DIGITE UM NUMERO DE 6 A 20"; N

35 REM TESTA SE O NUMERO E DE 6 A 20
40 IF N<6 OR N>20 THEN GOTO 30

44 REM RESETA CONTADOR DE TEMPO
45 TIME = 0

46 REM CRIA UM VETOR PARA ARMAZENAR OS NUMEROS GERADOS
50 DIM A(N-1)

55 REM LOOP PARA GERAR NUMEROS SEM SEREM REPETIDOS
60 FOR I=0 TO N-1
70     A(I)=INT(RND(-TIME)*60)
80     FOR T=0 TO N-1
90         IF I<>T THEN IF A(I)=A(T) THEN GOTO 70
100    NEXT T
110 NEXT I

115 REM LOOP PARA COLOCAR OS NUMEROS EM ORDEM DENTRO DO VETOR
120 FOR J=1 TO N-1
130     FLAG=0
140     FOR I=0 TO N-1-J
150         IF A(I)>A(I+1) THEN AUX=A(I):A(I)=A(I+1):A(I+1)=AUX:FLAG=1
160     NEXT I
170     IF FLAG=0 THEN GOTO 190
180 NEXT J

185 REM APRESENTAR O RESULTADO
190 PRINT "BOA SORTE. SEUS NUMEROS SAO: "
200 FOR I=0 TO N-1
210     PRINT A(I)
220 NEXT I

225 REM MOSTRAR O TEMPO GASTO PARA GERAR OS NUMEROS
230 PRINT "TEMPO: "; USING"##.#";TIME/60


