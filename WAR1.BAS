10 KEY OFF
80 NT=10: REM NUMERO DE TROPAS DE CADA LADO
90 ES=6: REM ESPALHAMENTO DAS TROPAS
100 REM MONTAR O TABULEIRO
110 X = RND(-TIME) : REM INICIALIZA GERADOR ALEATORIO
120 CLS
130 LOCATE 13,0: PRINT "*** W A R ***": PRINT
140 LOCATE 2,2: PRINT "  1  2  3  4  5  6  7  8"
150 L=4
160 FOR I=1 TO 8
170     LOCATE 0, L: PRINT I
180     L=L+2
190 NEXT I

200 REM DEFINIR AS MATRIZES
210 DIM V$(8,8), F$(8,8)
220 DIM TX(20), TY(20): TC=0

300 REM INICIALIZAR MATRIZ VAZIA
310 FOR I=1 TO 8
320     FOR J=1 TO 8
330         V$(I,J)="."
340     NEXT J
350 NEXT I

400 REM BASE AMIGA (O) - CANTO SUPERIOR ESQUERDO
410 OX=INT(RND(1)*2)+1 : OY=INT(RND(1)*2)+1
420 V$(OX,OY)="O"

500 REM TROPAS AMIGAS (T) - 5 UNIDADES (GARANTIDAS)
510 TCOUNT=0
520 REM LOOP PARA COLOCAR TROPAS AMIGAS
530 TI=OX+INT(RND(1)*ES)-2 : REM RAIO MAIOR (-2 a +2)
540 TJ=OY+INT(RND(1)*ES)-2
550 IF TI<1 OR TI>8 OR TJ<1 OR TJ>8 THEN 580
560 IF V$(TI,TJ)="." THEN V$(TI,TJ)="T" : TCOUNT=TCOUNT+1
580 IF TCOUNT<NT THEN 530

700 REM BASE INIMIGA (X) - CANTO INFERIOR DIREITO
710 XX=INT(RND(1)*2)+7 : XY=INT(RND(1)*2)+7
720 V$(XX,XY)="X"

800 REM TROPAS INIMIGAS (Y) - 5 UNIDADES (GARANTIDAS)
810 YCOUNT=0
820 REM LOOP PARA COLOCAR TROPAS INIMIGAS
830 YI=XX+INT(RND(1)*ES)-2 : REM RAIO MAIOR (-2 a +2)
840 YJ=XY+INT(RND(1)*ES)-2
850 IF YI<1 OR YI>8 OR YJ<1 OR YJ>8 THEN 880
860 IF V$(YI,YJ)="." THEN V$(YI,YJ)="Y" : YCOUNT=YCOUNT+1
880 IF YCOUNT<NT THEN 830

1000 REM BARREIRAS (B) - 5 UNIDADES
1010 BCOUNT=0
1020 FOR I=1 TO 8
1030     FOR J=1 TO 8
1040         IF V$(I,J)<>"." THEN 1070
1050         IF RND(1)>0.15 THEN 1070 : REM 15% DE CHANCE
1060         IF BCOUNT<5 THEN V$(I,J)="B" : BCOUNT=BCOUNT+1
1070     NEXT J
1080 NEXT I
1090 IF BCOUNT<5 THEN 1020

1100 REM MOSTRAR TABULEIRO
1110 FOR I=1 TO 8
1120     FOR J=1 TO 8
1130         LOCATE J*3+1, I*2+2
1140         PRINT V$(I,J)
1150     NEXT J
1160 NEXT I

1170 GOSUB 12000

1200 REM LOOP PRINCIPAL DO JOGO
1201 GO=1
1202 IF GO=0 THEN 1210
1203 GOSUB 14000: REM TURNO DO JOGADOR
1204 GOSUB 15000: REM TURNO DO COMPUTADOR
1205 GOTO 1202
1210 END







10000 REM SUB-ROTINAS

11000 REM MOSTRAR TABULEIRO
11010 FOR I=1 TO 8
11020     FOR J=1 TO 8
11030         LOCATE J*3+1, I*2+2
11040         PRINT V$(I,J)
11050     NEXT J
11060 NEXT I
11070 RETURN

12000 REM SUBROTINA PARA CALCULAR MATRIZ DE FORÇA (F$) - VERSÃO CORRIGIDA
12010 FOR I=1 TO 8
12020     FOR J=1 TO 8
12030         F$(I,J)="0"
12040         IF V$(I,J)="O" THEN GOSUB 12500
12050         IF V$(I,J)="T" THEN GOSUB 12600
12060         IF V$(I,J)="X" THEN GOSUB 12700
12070         IF V$(I,J)="Y" THEN GOSUB 12800
12080     NEXT J
12090 NEXT I
12100 RETURN

12500 REM BASE AMIGA (O) - FORÇA = 2 + T's ADJACENTES
12510 COUNT=2
12520 GOSUB 12900 : REM CONTA TROPAS T AO REDOR
12530 F$(I,J)=STR$(COUNT)
12540 RETURN

12600 REM TROPA AMIGA (T) - FORÇA = 1 + T's ADJACENTES (INCLUINDO SI MESMA)
12610 COUNT=0
12620 GOSUB 12900 : REM CONTA TROPAS T AO REDOR (INCLUINDO PRÓPRIA POSIÇÃO)
12630 F$(I,J)=STR$(COUNT)
12640 RETURN

12700 REM BASE INIMIGA (X) - FORÇA = 2 + Y's ADJACENTES
12710 COUNT=2
12720 GOSUB 13000 : REM CONTA TROPAS Y AO REDOR
12730 F$(I,J)=STR$(COUNT)
12740 RETURN

12800 REM TROPA INIMIGA (Y) - FORÇA = 1 + Y's ADJACENTES (INCLUINDO SI MESMA)
12810 COUNT=0
12820 GOSUB 13000 : REM CONTA TROPAS Y AO REDOR (INCLUINDO PRÓPRIA POSIÇÃO)
12830 F$(I,J)=STR$(COUNT)
12840 RETURN

12900 REM SUBROTINA PARA CONTAR TROPAS T
12910 FOR DI=-1 TO 1
12920     FOR DJ=-1 TO 1
12930         NI=I+DI : NJ=J+DJ
12940         IF NI<1 OR NI>8 OR NJ<1 OR NJ>8 THEN 12970
12950         IF V$(NI,NJ)="T" THEN COUNT=COUNT+1
12960         REM INCLUI A PRÓPRIA POSIÇÃO NO CÁLCULO
12970     NEXT DJ
12980 NEXT DI
12990 RETURN

13000 REM SUBROTINA PARA CONTAR TROPAS Y
13010 FOR DI=-1 TO 1
13020     FOR DJ=-1 TO 1
13030         NI=I+DI : NJ=J+DJ
13040         IF NI<1 OR NI>8 OR NJ<1 OR NJ>8 THEN 13070
13050         IF V$(NI,NJ)="Y" THEN COUNT=COUNT+1
13060         REM INCLUI A PRÓPRIA POSIÇÃO NO CÁLCULO
13070     NEXT DJ
13080 NEXT DI
13090 RETURN

14000 REM SUBROTINA PARA MOVIMENTAR TROPA (JOGADOR)
14010 LOCATE 0,20: PRINT "Seu turno:                  "
14020 LOCATE 0,21: PRINT "Linha da tropa (1-8):       ": LOCATE 22,21: INPUT ""; L1
14030 LOCATE 0,22: PRINT "Coluna da tropa (1-8):      ": LOCATE 22,22: INPUT ""; C1
14040 IF L1<1 OR L1>8 OR C1<1 OR C1>8 THEN LOCATE 0,23: PRINT "Coordenadas invalidas!      ": GOTO 14020
14050 IF V$(L1,C1)<>"T" AND V$(L1,C1)<>"O" THEN LOCATE 0,23: PRINT "Nao eh sua tropa!           ": GOTO 14020
14060 LOCATE 0,21: PRINT "Nova linha (1-8):           ": LOCATE 22,21: INPUT ""; L2
14070 LOCATE 0,22: PRINT "Nova coluna (1-8):          ": LOCATE 22,22: INPUT ""; C2
14080 IF L2<1 OR L2>8 OR C2<1 OR C2>8 THEN LOCATE 0,23: PRINT "Coordenadas invalidas!      ": GOTO 14060
14090 REM VERIFICA SE MOVIMENTO EH ADJACENTE
14100 IF ABS(L2-L1)>1 OR ABS(C2-C1)>1 THEN LOCATE 0,23: PRINT "Movimento nao adjacente!    ": GOTO 14060
14110 REM VERIFICA SE POSICAO DESTINO EH VALIDA
14120 IF V$(L2,C2)="B" THEN LOCATE 0,23: PRINT "Barreira no caminho!        ": GOTO 14060
14130 IF V$(L2,C2)="T" OR V$(L2,C2)="O" THEN LOCATE 0,23: PRINT "Tropa aliada no destino!    ": GOTO 14060
14140 REM VERIFICA SE HA COMBATE
14150 IF V$(L2,C2)="Y" OR V$(L2,C2)="X" THEN OL=L1:OC=C1:NL=L2:NC=C2:GOSUB 16000: GOTO 14170
14160 REM MOVIMENTO SIMPLES
14165 V$(L2,C2)=V$(L1,C1): V$(L1,C1)="."
14170 GOSUB 12000: REM RECALCULA FORCAS
14180 GOSUB 11000: REM REDESENHA TABULEIRO
14190 LOCATE 0,20: PRINT "                              "
14200 LOCATE 0,21: PRINT "                              "
14210 LOCATE 0,22: PRINT "                              "
14220 RETURN

15000 REM SUBROTINA PARA MOVIMENTAR TROPA INIMIGA (COMPUTADOR)
15010 LOCATE 0,20: PRINT "Turno do computador...       "
15020 REM ENCONTRA TODAS AS TROPAS INIMIGAS
15030 TC=0
15040 FOR I=1 TO 8
15050     FOR J=1 TO 8
15060         IF V$(I,J)="Y" THEN TC=TC+1: TX(TC)=I: TY(TC)=J
15070     NEXT J
15080 NEXT I
15090 IF TC=0 THEN RETURN: REM NENHUMA TROPA INIMIGA
15100 REM SELECIONA TROPA ALEATORIA PARA MOVER
15110 T=INT(RND(1)*TC)+1
15120 OX=TX(T): OY=TY(T)
15130 REM TENTA MOVER EM TODAS AS DIRECOES POSSIVEIS
15140 FOR TR=1 TO 8
15150 D=INT(RND(1)*8)+1
15160 IF D=1 THEN NL=OX-1: NC=OY-1
15170 IF D=2 THEN NL=OX-1: NC=OY
15180 IF D=3 THEN NL=OX-1: NC=OY+1
15190 IF D=4 THEN NL=OX: NC=OY-1
15200 IF D=5 THEN NL=OX: NC=OY+1
15210 IF D=6 THEN NL=OX+1: NC=OY-1
15220 IF D=7 THEN NL=OX+1: NC=OY
15230 IF D=8 THEN NL=OX+1: NC=OY+1
15240 IF NL<1 OR NL>8 OR NC<1 OR NC>8 THEN 15330
15250 IF V$(NL,NC)="B" THEN 15330
15260 IF V$(NL,NC)="Y" OR V$(NL,NC)="X" THEN 15330
15270 REM SE EH MOVIMENTO VALIDO OU COMBATE
15280 IF V$(NL,NC)="T" OR V$(NL,NC)="O" THEN OL=OX:OC=OY:NL=NL:NC=NC:GOSUB 16000: RETURN
15290 V$(NL,NC)=V$(OX,OY)
15300 V$(OX,OY)=".": GOSUB 12000: GOSUB 11000: RETURN
15330 NEXT TR
15340 LOCATE 0,21: PRINT "Computador nao moveu        "
15350 RETURN

16000 REM SUBROTINA PARA DETECTAR UM ATAQUE
16010 LOCATE 0,20: PRINT "Combate iniciado!           "
16020 A$=V$(OL,OC): D$=V$(NL,NC)
16030 F1=VAL(F$(OL,OC)): F2=VAL(F$(NL,NC))
16040 REM SALVA POSICOES PARA ATUALIZACAO
16050 AL=OL: AC=OC: DL=NL: DC=NC
16060 GOSUB 17000: REM CALCULA VITORIA
16070 IF VT<>1 THEN 16120
16080 V$(DL,DC)=A$: V$(AL,AC)="."
16090 IF D$="O" THEN LOCATE 0,21: PRINT "VOCE VENCEU O JOGO!        ": LOCATE 0,22: PRINT "                              ": END
16100 IF D$="X" THEN LOCATE 0,21: PRINT "COMPUTADOR VENCEU!         ": LOCATE 0,22: PRINT "                              ": END
16110 GOTO 16160
16120 V$(AL,AC)="."
16130 IF A$="O" THEN LOCATE 0,21: PRINT "VOCE PERDEU O JOGO!        ": LOCATE 0,22: PRINT "                              ": END
16140 IF A$="X" THEN LOCATE 0,21: PRINT "COMPUTADOR PERDEU!         ": LOCATE 0,22: PRINT "                              ": END
16160 GOSUB 12000: REM RECALCULA FORCAS
16170 GOSUB 11000: REM REDESENHA TABULEIRO
16180 RETURN

17000 REM SUBROTINA PARA CALCULAR TROPA VITORIOSA
17010 REM F1 = FORCA ATACANTE, F2 = FORCA DEFENSOR
17020 REM CALCULA CHANCE DE VITORIA (60% + DIFERENCA DE FORCA * 10%)
17030 DF=F1-F2
17040 CH=60+DF*10
17050 IF CH<10 THEN CH=10
17060 IF CH>90 THEN CH=90
17070 R=INT(RND(1)*100)+1
17080 IF R>CH THEN 17120
17090 VT=1
17100 LOCATE 0,21: PRINT A$;"(";F1;") vs ";D$;"(";F2;") - Venceu ";A$;"!"
17105 LOCATE 0,22: PRINT "                              "
17110 RETURN
17120 VT=0
17130 LOCATE 0,21: PRINT A$;"(";F1;") vs ";D$;"(";F2;") - Venceu ";D$;"!"
17135 LOCATE 0,22: PRINT "                              "
17140 RETURN





50000 REM L=7:C=8:PRINT V$(L,C);" ";F$(L,C)
